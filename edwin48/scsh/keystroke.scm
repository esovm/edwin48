;;; -*- Mode: Scheme; scheme48-package: keystroke -*-
;;;
;;; MIT Scheme-like chars (with bucky bits)
;;;
;;; The bucky-bits field

;;; also defined for-syntax, see packages.scm
;;;
(define *keystroke-modifiers* '())
(define *keystroke-prefix*    'keystroke-modifier:)

(define-syntax define-keystroke-modifier
  (lambda (form rename compare)
    (let* ((form      (cdr form))
           (name      (car form))
           (offset    (cadr form))
           (full-name (symbol-append *keystroke-prefix* name))
           (%define   (rename 'define)))
      (set! *keystroke-modifiers* (alist-cons name offset *keystroke-modifiers*))
      `(,%define ,full-name ,offset))))

(define-keystroke-modifier meta    1)
(define-keystroke-modifier control 2)
(define-keystroke-modifier super   4)
(define-keystroke-modifier hyper   8)

(define-record-type* keystroke
  (make-keystroke
   ;;
   ;; values store the string sequence generated by the keystroke
   ;; If it's a single character, then it's a string of length 1.
   ;;
   value
   ;;
   ;; modifiers must be one of the defined keystroke modifiers, listed
   ;; above
   ;;
   modifiers)
  ())

(define-syntax kbd
  (lambda (form rename compare)
    (let ((%keystroke (rename 'make-keystroke))
          (r          rename)
          (form       (cdr form))) ;; discard the first token, 'KBD'
      (define (parse-kbd-form form)
        (cond
         ((char? form)
          `(,%keystroke (,(r 'string) ,form) 0))
         ((string? form)
          (if (= 1 (string-length form))
              `(,%keystroke ,form 0)
              `(,(r 'map) (,(r 'lambda) (c) (,%keystroke (,(r 'string) c) 0))
                (,(r 'string->list) ,form))))
         ((list? form)
          (let* ((form      (reverse form))
                 (key       (car form))
                 (modifiers (cdr form)))
            ;; if all the modifiers are valid
            (if (lset<= compare modifiers
                        (map (lambda (x) (rename (car x))) *keystroke-modifiers*))
                `(,%keystroke ,key (,(r 'delete-duplicates) ',modifiers))
                (syntax-error "contains invalid modifier" modifiers))))))
      (if (= 1 (length form))
          (parse-kbd-form (car form))
          (map parse-kbd-form form)))))


